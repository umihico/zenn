---
title: "æ¯æ—¥æœ¬ç•ªDBã‚’ãƒ€ãƒ³ãƒ—ã—ã¦ã€ãƒ­ãƒ¼ã‚«ãƒ«ã¨é–‹ç™ºç’°å¢ƒã§åˆ©ç”¨ã—ã¦ç”Ÿç”£æ€§ã‚’ä¸Šã’ã¦ã‚‹è©±"
emoji: "ğŸ’¿"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["githubactions", "rails", "terraform", "aurora", "aws"] # è¨˜äº‹æ¤œç´¢ã—ã¦é–¢é€£ã™ã‚‹ãƒˆãƒ”ãƒƒã‚¯ã‚’è¦‹ã¤ã‘ã‚‹ã¨ã€Œã“ã®ãƒˆãƒ”ãƒƒã‚¯ã‚’æŒ‡å®šã™ã‚‹ã«ã¯â—¯â—¯â—¯ã¨å…¥åŠ›ã€ã¨æ•™ãˆã¦ãã‚Œã‚‹
publication_name: "game8_blog"
published: true # äºˆç´„æŠ•ç¨¿ã§ã¯trueã‚’æŒ‡å®šã™ã‚‹(https://zenn.dev/zenn/articles/zenn-cli-guide)
# published_at: 2024-02-16 09:00
---

**ã‚·ãƒ¼ãƒ‰ãƒ‡ãƒ¼ã‚¿ã§å‹•ä½œç¢ºèªã—ã¦å¤§ä¸ˆå¤«ã ã£ãŸã®ã«ã€æœ¬ç•ªåæ˜ ã—ã¦ã¿ãŸã‚‰æƒ³å®šã—ã¦ãªã‹ã£ãŸæŒ™å‹•ãƒ»ã‚¨ãƒ©ãƒ¼ãŒå‡ºãŸğŸ˜±ãã‚“ãªçµŒé¨“ã¯ã‚ã‚Šã¾ã›ã‚“ã‹ã€‚**

æ¥ãšã‹ã—ãªãŒã‚‰ç§ã¯ä»Šã¾ã§ã«ä½•å›ã‚‚ã‚ã‚Šã¾ã—ãŸã€‚æ©Ÿèƒ½é–‹ç™ºã ã‘ã˜ã‚ƒãªããƒãƒƒãƒã‚„ãƒã‚¤ã‚°ãƒ¬ãƒ¼ã‚·ãƒ§ãƒ³ãªã‚“ã‹ã§ã‚‚ç™ºç”Ÿã—ãŒã¡ãªã‚³ãƒ¬ã€‚ã¾ãŸã¯ã‚·ãƒ¼ãƒ‰ãƒ‡ãƒ¼ã‚¿ã§å‹•ä½œç¢ºèªã§ãã¦ã‚‚ã€æœ¬ç•ªãƒ‡ãƒ¼ã‚¿ã§ã‚‚é€šç”¨ã™ã‚‹ã‹æ¤œè¨¼ãŒã§ããªã„ã¾ã¾ãƒ—ãƒ«ãƒªã‚¯ã‚’ä½œã‚‹ã€ãªã‚“ã¦ã„ã†ã“ã¨ã‚‚ã‚ã‚‹ã¨æ€ã„ã¾ã™ã€‚ä»Šå›ã¯ã“ã¡ã‚‰ã‚’ç„¡ãã™è©¦ã¿ã‚’ã—ãŸãŠè©±ã§ã™ã€‚

# ã€Œã‚‚ã†æœ¬ç•ªDBã§é–‹ç™ºã—ã¡ã‚ƒãˆã°ã„ã„ã˜ã‚ƒãªã„ã€ã®å•é¡Œç‚¹

ã“ã®èª²é¡Œã‚’è§£æ±ºã™ã‚‹ã«ã¯ã€æ¥µè«–ã™ã‚‹ã¨æœ¬ç•ªDBã§é–‹ç™ºã™ã‚‹ã—ã‹ãªã„ã®ã§ã™ãŒã€ãã†ãªã‚‹ã¨è¨€ã†ã¾ã§ã‚‚ãªãä»¥ä¸‹ã®å•é¡ŒãŒå‡ºã¦ãã¾ã™ã€‚

- ãƒ¬ãƒ“ãƒ¥ãƒ¼é€šéã—ã¦ãªã„ã‚³ãƒ¼ãƒ‰ãŒæœ¬ç•ªã«å½±éŸ¿ã‚’ä¸ãˆã‚‹
- ãƒˆãƒ©ã‚¤ï¼†ã‚¨ãƒ©ãƒ¼ãŒã§ããªã„
- å€‹äººæƒ…å ±ã‚’ã¯ã˜ã‚ã¨ã™ã‚‹ã‚»ãƒ³ã‚·ãƒ†ã‚£ãƒ–ãªæƒ…å ±ãŒé–‹ç™ºè€…ã®ç«¯æœ«ã«æ¼ã‚Œã‚‹
- ãƒ‡ãƒ¼ã‚¿é‡ãŒå¤šã™ãã¦ãƒ­ãƒ¼ã‚«ãƒ«ã«æŒã£ã¦ã“ã‚Œãªã„

ã—ã‹ã—è¨€ã„æ›ãˆã‚‹ã¨ã€ã“ã‚Œã‚‰ã‚’ã‚¯ãƒªã‚¢ã—ãŸDBã•ãˆç”¨æ„ã§ãã‚Œã°è‰¯ã„ã‚ã‘ã§ã™ã€‚

# è§£æ±ºç­–ã¨ã—ã¦ãƒã‚¤ãƒ³ãƒˆã‚¤ãƒ³ã‚¿ã‚¤ãƒ ãƒªã‚«ãƒãƒªã¨ã€ãƒã‚¹ã‚­ãƒ³ã‚°ã—ã¦ãƒ€ãƒ³ãƒ—ã™ã‚‹ãƒãƒƒãƒå‡¦ç†

ã‚²ãƒ¼ãƒ ã‚¨ã‚¤ãƒˆã§ç«‹ã¡ä¸Šã’ãŸ[PLAYZY](https://playzy.jp)ã¨[ãƒã‚¤ã‚²ãƒ¼ãƒ ã‚¨ã‚¤ãƒˆ](https://mygame8.jp)ã§ã¯ã€Amazon Aurora(Mysql)ã‚’ä½¿ã£ã¦ãŠã‚Šã€æœ¬ç•ªDBã‚’æ¯æœãƒã‚¤ãƒ³ãƒˆã‚¤ãƒ³ã‚¿ã‚¤ãƒ ãƒªã‚«ãƒãƒªã—ã¦ã‚¯ãƒ­ãƒ¼ãƒ³ã—ã¦ã„ã¾ã™ã€‚ã¤ã¾ã‚Šã“ã®æ™‚ç‚¹ã§ã¯æœ¬ç•ªã¨é–‹ç™ºç’°å¢ƒã¨æœ¬ç•ªã‚’ã‚¯ãƒ­ãƒ¼ãƒ³ã—ãŸï¼“ã¤ã®DBã‚¯ãƒ©ã‚¹ã‚¿ãƒ¼ãŒå­˜åœ¨ã—ã¦ã„ã¾ã™ã€‚ãã—ã¦ãã®ã‚¯ãƒ­ãƒ¼ãƒ³ã—ãŸDBã«å¯¾ã—ã¦ã€åŒæ™‚ã«ç«‹ã¡ä¸Šã’ãŸEC2ã§ä»¥ä¸‹ã®ãƒãƒƒãƒå‡¦ç†ã‚’è¡Œã„ã€ãƒ­ãƒ¼ã‚«ãƒ«ã¨é–‹ç™ºç’°å¢ƒã§æœ¬ç•ªDBåŒç­‰ç‰©ãŒåˆ©ç”¨ã§ãã‚‹ã‚ˆã†ã«ã—ã¦ã„ã¾ã™ã€‚ï¼ˆã‚³ãƒ¼ãƒ‰ã¯æœ€å¾Œã«ç´¹ä»‹ã—ã¾ã™ã€‚ï¼‰

- ã‚»ãƒ³ã‚·ãƒ†ã‚£ãƒ–ãªæƒ…å ±ã‚’ãƒã‚¹ã‚­ãƒ³ã‚°ã™ã‚‹SQLã‚’æµã™
- ãƒã‚¹ã‚­ãƒ³ã‚°å¾Œã«mysqldumpã§dumpã€‚ã“ã®ã¨ãã«é‡ã„ãƒ†ãƒ¼ãƒ–ãƒ«ã¯é™¤ã
- ãƒ€ãƒ³ãƒ—ã—ãŸSQLãƒ•ã‚¡ã‚¤ãƒ«ã‚’S3ã«ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰
- ãƒ€ãƒ³ãƒ—ã—ãŸSQLãƒ•ã‚¡ã‚¤ãƒ«ã‚’é–‹ç™ºç’°å¢ƒDBã«æµã™
- ã‚¯ãƒ­ãƒ¼ãƒ³ã•ã‚ŒãŸDBã‚¯ãƒ©ã‚¹ã‚¿ãƒ¼ã‚’å‰Šé™¤

ã“ã‚Œã«ã¦é–‹ç™ºç’°å¢ƒã®DBã¯æ¯æœæœ€æ–°çŠ¶æ…‹ã«ãƒªã‚»ãƒƒãƒˆã•ã‚Œã€ãƒ­ãƒ¼ã‚«ãƒ«ã¯S3ã‹ã‚‰SQLãƒ•ã‚¡ã‚¤ãƒ«ã‚’æŒã£ã¦ãã¦æµã™ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’ç”¨æ„ã—ã¦å®Ÿè¡Œã™ã‚‹ã“ã¨ã§ãƒ­ãƒ¼ã‚«ãƒ«ã«ã‚¤ãƒ³ãƒãƒ¼ãƒˆã—ã¦ã„ã¾ã™ã€‚

# å‹•ä½œç¢ºèªã®è³ªãŒå‘ä¸Šã™ã‚‹ã»ã‹ã€æ•°å¤šãã®ãƒ¡ãƒªãƒƒãƒˆãŒç™ºç”Ÿã€‚ä¸€éƒ¨ãƒ‡ãƒ¡ãƒªãƒƒãƒˆã‚‚

æœ¬ç•ªãƒ‡ãƒ¼ã‚¿åŒç­‰ç‰©ã‚’ãƒ­ãƒ¼ã‚«ãƒ«ã¨é–‹ç™ºç’°å¢ƒã§ä¿æœ‰ã§ãã‚‹ã‚ˆã†ã«ãªã£ãŸã“ã¨ã§ã€ä»¥ä¸‹ã®ãƒ¡ãƒªãƒƒãƒˆãŒã‚ã‚Šã¾ã—ãŸã€‚

- å®Ÿè£…æ™‚ã®ãƒã‚°ãƒ»ä¸å®‰ãŒæ¸›ã‚‹
- ãƒ¬ãƒ“ãƒ¥ãƒ¼æ™‚ã®ä¸å®‰ãŒæ¸›ã‚‹
- ã‚¨ãƒ©ãƒ¼ãŒèµ·ããŸå®Ÿéš›ã®ãƒ¦ãƒ¼ã‚¶ãƒ¼ã‚„ã‚³ãƒ³ãƒ†ãƒ³ãƒ„ãªã©ã§ç›´æ¥å‹•ä½œç¢ºèªãŒã§ãã€å¯¾å¿œã‚¹ãƒ”ãƒ¼ãƒ‰ãŒä¸ŠãŒã‚‹
- ç¤¾å†…å±•é–‹ã™ã‚‹é–‹ç™ºç’°å¢ƒã®æœ¬ç•ªã¨ã®ä¹–é›¢ãŒï¼ˆã»ã¼ï¼‰ãªããªã‚‹
- Github Actionsã‚’ghã‚³ãƒãƒ³ãƒ‰ã§dispatchã™ã‚‹ã“ã¨ã§ã€ã‚¨ãƒ©ãƒ¼ãŒèµ·ããŸãƒ‡ãƒ¼ã‚¿ãŒç”Ÿæˆã•ã‚ŒãŸç›´å¾Œã§ã‚‚ã‚¯ãƒ­ãƒ¼ãƒ³ã—ã¦å¯¾å¿œå¯èƒ½

ãƒ‡ãƒ¡ãƒªãƒƒãƒˆã¯ä»¥ä¸‹ã®ã¨ãŠã‚Šã§ã™ã€‚

- ã‚·ãƒ¼ãƒ‰ãƒ‡ãƒ¼ã‚¿ã‚’ãƒ¡ãƒ³ãƒ†ã™ã‚‹ãƒ¡ãƒªãƒƒãƒˆãŒå°ã•ããªã‚Šã€è³ªã‚„ãƒ¢ãƒãƒ™ãŒä¸‹ãŒã‚‹
- ãƒã‚¹ã‚­ãƒ³ã‚°ã—ãŸãƒ‡ãƒ¼ã‚¿ã¯ã‹ãˆã£ã¦ãƒ‡ãƒ¼ã‚¿ã®å¤šæ§˜æ€§ã‚’ä¸‹ã’ã‚‹ã“ã¨ã«ãªã‚ŠãŒã¡
- ãƒ†ãƒ¼ãƒ–ãƒ«ã‚’å¤–ã™ã®ã§ãªãæ¸›ã‚‰ã™ã—ã‹ãªã„ã¨ãªã‚‹ã¨ã€å‡¦ç†å·¥æ•°ãŒè†¨ã‚ŒãŒã¡
- ç”»åƒãªã©DBå¤–ã®ã‚³ãƒ³ãƒ†ãƒ³ãƒ„ã®å‚ç…§ãªã©ã€DBã‚’å…¥ã‚Œæ›¿ãˆã‚‹ã¨å‚ç…§ã§ããªããªã‚‹ãƒ­ã‚¸ãƒƒã‚¯ãŒã‚ã‚‹ã¨ã€åˆæœŸã«å¯¾å¿œãŒå¿…è¦ã«ãªã‚‹
- terraformã§å®Ÿç¾ã—ã¦ã¾ã™ãŒã€ä»Šã®ã¨ã“ã‚Github Actionsä¸Šã§applyã—ã¦ã‚‹ãŸã‚ã€DBã‚¯ãƒ©ã‚¹ã‚¿ãƒ¼ã®ä½œæˆã§èª²é‡‘ãŒã‹ã‹ã‚‹ï¼ˆæ¯æœï¼“ï¼åˆ†ãã‚‰ã„ï¼‰

ãƒ‡ãƒ¡ãƒªãƒƒãƒˆã¯ã‚ã‚‹ã‚‚ã®ã®ã€ä¹—ã‚Šè¶ŠãˆãŸã‚‰å¾—ã‚‰ã‚Œã‚‹ãƒ¡ãƒªãƒƒãƒˆã®æ–¹ãŒåœ§å€’çš„ã«å¤§ãã„ã¨æ€ã£ã¦ã„ã¾ã™ã€‚ã‚²ãƒ¼ãƒ ã‚¨ã‚¤ãƒˆã®å‰è·ã‹ã‚‰å°å…¥ã—ãŸæ‰‹æ³•ã§ã™ãŒã€ç¾åœ¨æ‹…å½“ã—ã¦ã‚‹ãƒ—ãƒ­ãƒ€ã‚¯ãƒˆã«ã¯å…¨éƒ¨å…¥ã‚Œã¾ã—ãŸã—ã€ä»Šå¾Œã‚‚å¯èƒ½ãªã¨ã“ã‚ã§ã¯å¸¸ã«å°å…¥ã—ã¦ã„ããŸã„ã¨æ€ã£ã¦ã„ã¾ã™ã€‚

# å®Ÿè£…ã¯Github Actionsã¨Terraform

ä»¥ä¸‹ã®terraformã®ã‚³ãƒ¼ãƒ‰ã‚’æ¯æœGithub Actionsã§ã‚¹ã‚±ã‚¸ãƒ¥ãƒ¼ãƒ«ã—ã¦å®Ÿè¡Œã—ã¦ã„ã¾ã™ã€‚

## DBã‚¯ãƒ©ã‚¹ã‚¿ãƒ¼ã‚’ä½œæˆã™ã‚‹ãƒ–ãƒ­ãƒƒã‚¯

```terraform
resource "random_string" "mysql_password" {
  length  = 16
  special = false
}

module "restored-prod-rds" {
  source  = "terraform-aws-modules/rds-aurora/aws"
  version = "9.0.0"

  manage_master_user_password = false
  name                        = "restored-prod-temp"
  engine                      = "aurora-mysql"
  engine_version              = "5.7.mysql_aurora.2.11.2"
  instance_class              = "db.t3.small"
  restore_to_point_in_time = {
    source_cluster_identifier  = "prod"
    use_latest_restorable_time = true
  }
  instances = {
    one = {
      promotion_tier = 1
    }
  }
  vpc_id                 = var.vpc_id
  subnets                = var.private_subnets
  create_db_subnet_group = true
  security_group_rules = {
    ex1_ingress = {
      cidr_blocks = []
    }
    ex1_ingress = {
      source_security_group_id = var.default_security_group_id
    }
  }
  security_group_description = "Managed by Terraform"
  skip_final_snapshot        = true
  storage_encrypted          = true
  monitoring_interval        = 60
  deletion_protection        = false
  autoscaling_enabled        = false
  publicly_accessible        = false
  master_password            = random_string.mysql_password.result

  db_parameter_group_name         = var.parameters.name
  db_cluster_parameter_group_name = var.cluster_parameters.name
  ca_cert_identifier              = "rds-ca-rsa2048-g1"
}
```

## DBã‚¯ãƒ©ã‚¹ã‚¿ãƒ¼ã®å‰Šé™¤ã¨S3ã¸ã®ãƒ€ãƒ³ãƒ—ãƒ•ã‚¡ã‚¤ãƒ«ãŒã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ã§ãã‚‹ã‚ˆã†ã«ã€ç«‹ã¡ä¸Šã’ã‚‹EC2ã®æ¨©é™ã‚’è¨­å®š

```terraform
resource "aws_iam_role" "ec2" {
  name = "restored-rds-worker-role"
  assume_role_policy = jsonencode({
    "Version" : "2012-10-17"
    "Statement" : [
      {
        "Action" : "sts:AssumeRole"
        "Effect" : "Allow"
        "Principal" : {
          "Service" : ["ec2.amazonaws.com"]
        },
      },
    ]
  })
  inline_policy {
    name = "restored-rds-worker-role-policy"
    policy = jsonencode({
      "Version" : "2012-10-17"
      "Statement" : [
        {
          "Action" : "s3:PutObject"
          "Effect" : "Allow"
          "Resource" : ["arn:aws:s3:::${var.s3_bucket_id}/database_dump.sql"]
        },
        {
          "Effect" : "Allow",
          "Action" : [
            "rds:DeleteDBCluster",
            "rds:DescribeDBClusters",
            "rds:DeleteDBInstance",
          ],
          "Resource" : [module.restored-prod-rds.cluster_arn, module.restored-prod-rds.cluster_instances["one"].arn]
        }
      ]
    })
  }
  managed_policy_arns = [
    "arn:aws:iam::aws:policy/AmazonSSMManagedInstanceCore" # ãƒ‡ãƒãƒƒã‚°ç”¨ã«SSHæ¥ç¶šã§ãã‚‹ã‚ˆã†ã«
  ]
}

resource "aws_iam_instance_profile" "this" {
  name = "restored-rds-worker-instance-profile"
  role = aws_iam_role.ec2.name
}

```

## ãƒã‚¹ã‚­ãƒ³ã‚°ãƒ»ãƒ€ãƒ³ãƒ—ãƒ»ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ãƒ»ã‚¤ãƒ³ãƒãƒ¼ãƒˆã‚’è¡Œã†EC2æœ¬ä½“

èµ·å‹•æ™‚ã«å®Ÿè¡Œã•ã‚Œã‚‹ã‚¹ã‚¯ãƒªãƒ—ãƒˆã¨ã—ã¦user dataã‚’æ¸¡ã—ã¦ã„ã¾ã™ã€‚

```terraform

data "aws_ssm_parameter" "latest_amazonlinux_2023" {
  # aws ssm get-parameters --names /aws/service/ami-amazon-linux-latest/al2023-ami-kernel-default-x86_64
  # ã™ã‚‹ã¨å¾—ã‚‰ã‚Œã‚‹"ami-012261b9035f8f938"ã¯consoleã‹ã‚‰ç«‹ã¡ä¸Šã’æ™‚ã®ãƒ‡ãƒ•ã‚©ã¨ä¸€è‡´ã™ã‚‹

  # https://aws.amazon.com/jp/blogs/news/amazon-linux-2023-a-cloud-optimized-linux-distribution-with-long-term-support/ã‚ˆã‚Š
  name = "/aws/service/ami-amazon-linux-latest/al2023-ami-kernel-default-x86_64"
}

locals {
  user_data = <<-EOF
#!/bin/bash
# mysqlã‚¯ãƒ©ã‚¤ã‚¢ãƒ³ãƒˆã®ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«
sudo yum update -y
sudo rpm -Uvh https://dev.mysql.com/get/mysql80-community-release-el9-5.noarch.rpm
sudo yum install -y mysql-community-client

# ç–é€šç¢ºèªã€‚DBã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ã®ä½œæˆå®Œäº†ç›´å¾Œã«EC2ãŒç«‹ã¡ä¸ŠãŒã‚‹ã®ã§ä¸€å¿œ
while ! mysqladmin ping -h ${module.restored-prod-rds.cluster_endpoint} --silent; do
  sleep 1
done

# ãƒã‚¹ã‚­ãƒ³ã‚°å‡¦ç†
MYSQL_PWD=${random_string.mysql_password.result} mysql -h ${module.restored-prod-rds.cluster_endpoint} --user=root -e "UPDATE payout_users SET ${join(", ", [
  "name = CONCAT('å±±ç”°èŠ±å­', id)",
  "address = CONCAT('æ±äº¬éƒ½æ¸‹è°·åŒºæ±1-1-', id)",
  "email = CONCAT('user', id, '@example.com')",
  "postal_code = CONCAT('1', LPAD(id, 6, '0'))",
  "tel = CONCAT('080', LPAD(id, 8, '0'))",
])};" production

# ãƒ€ãƒ³ãƒ—
MYSQL_PWD=${random_string.mysql_password.result} mysqldump -h ${module.restored-prod-rds.cluster_endpoint} --user=root --set-gtid-purged=OFF production --ignore-table=production.versions --ignore-table=production.sessions --add-drop-database > /tmp/database_dump.sql

# S3ã«ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰
aws s3 cp /tmp/database_dump.sql s3://${var.s3_bucket_id}/database_dump.sql

# ãƒ€ãƒ³ãƒ—ãƒ•ã‚¡ã‚¤ãƒ«ã‚’é–‹ç™ºç’°å¢ƒDBã«ã‚¤ãƒ³ãƒãƒ¼ãƒˆ
cat /tmp/database_dump.sql | MYSQL_PWD=${var.rds_password} mysql -h ${var.cluster_endpoint} --user=root production

# CIã§å‰Šé™¤ã™ã‚‹ã¨CIå®Ÿè¡Œæ™‚é–“ï¼ˆã¤ã¾ã‚Šèª²é‡‘ï¼‰ãŒå¤§ãããªã‚‹ã®ã§ã€å‰Šé™¤ã¯AWS CLIã§è¡Œã†
aws rds delete-db-instance --db-instance-identifier ${module.restored-prod-rds.cluster_instances["one"].id}
aws rds delete-db-cluster --skip-final-snapshot --delete-automated-backups \
    --db-cluster-identifier ${module.restored-prod-rds.cluster_id}
EOF
}

resource "aws_instance" "this" {
  depends_on                  = [module.restored-prod-rds]
  ami                         = data.aws_ssm_parameter.latest_amazonlinux_2023.value
  user_data                   = base64encode(local.user_data)
  vpc_security_group_ids      = [var.default_security_group_id]
  subnet_id                   = var.private_subnets[0]
  instance_type               = "t3.nano"
  iam_instance_profile        = aws_iam_instance_profile.this.name
  associate_public_ip_address = true
  tags = {
    Name = "restored-rds-worker-instance"
  }
}
```

## Terraformã‚’å®Ÿè¡Œã™ã‚‹Github Actions

Github Actionsã§ã¯ãƒªã‚½ãƒ¼ã‚¹ã®applyã¨destroyã‚’ãã‚Œãã‚Œåˆ¥ã§ã‚¹ã‚±ã‚¸ãƒ¥ãƒ¼ãƒ«ã—ã¦ã„ã¦ã€ï¼‘æ™‚é–“å¾Œã«destroyã—ã¦EC2ã‚‚å‰Šé™¤ã—ã¦ã„ã¾ã™ã€‚ã“ã“ã¯ãƒ‡ãƒ¼ã‚¿é‡ã§æ™‚é–“é–“éš”ã‚„å®Ÿè¡Œé »åº¦ãŒèª¿æ•´ã•ã‚Œã‚‹ã¹ãã¨ã“ã‚ã§ã™ã€‚
ä½¿ã£ã¦ã„ã‚‹terraformã‚³ãƒãƒ³ãƒ‰ã¯applyã¨destroyã§ã¯ãªãä¸¡æ–¹applyã§ã‚ã‚Šã€ç’°å¢ƒå¤‰æ•°ã§ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®ä½œæˆã‚’æ¡ä»¶åˆ†å²ã—ã¦ãƒªã‚½ãƒ¼ã‚¹ã®apply/destroyã‚’åˆ¶å¾¡ã—ã¦ã„ã¾ã™ã€‚

ãªãŠã€ã‚¨ãƒ©ãƒ¼ã®åŸå› ã¨ãªã‚‹ãƒ‡ãƒ¼ã‚¿ãŒæœ¬ç•ªã§ç”Ÿã¾ã‚ŒãŸã°ã‹ã‚Šã¨ãªã‚‹ã¨ä»Šæœã®ãƒ‡ãƒ¼ã‚¿ã«ã¯æ®‹ã£ã¦ãªã„ã®ã§ã€
ghã‚³ãƒãƒ³ãƒ‰ã‚’å©ã„ã¦ãƒªã‚¢ãƒ«ã‚¿ã‚¤ãƒ ã§S3ã«ãƒ€ãƒ³ãƒ—ãƒ‡ãƒ¼ã‚¿ã‚’åãå‡ºã•ã›ã‚‹ã“ã¨ã‚‚ã§ãã¾ã™ã€‚

`gh workflow run update-dev-rds.yml -f step=restore`ã§DBã€EC2ã‚’ä½œã‚Šã€åæ˜ å¾Œã«ã¯`gh workflow run update-dev-rds.yml -f step=remove`ã§å‰Šé™¤ã—ã¦ã„ã¾ã™ã€‚

```yml
name: Update dev RDS

on:
  # gh workflow run update-dev-rds.yml -f step=restore
  workflow_dispatch:
    inputs:
      step:
        required: false
        default: "remove"
  schedule:
    - cron: "47 21 * * *" # 6:47am restore
    - cron: "47 22 * * *" # 7:47am remove restored

jobs:
  terraform:
    runs-on: ubuntu-latest
    env:
      APPLY_RESTORED_PROD_RDS: ${{ (github.event.schedule == '47 21 * * *' || github.event.inputs.step == 'restore') && 'yes' || 'no' }}
    steps:
      - uses: actions/checkout@v3
      - name: Setup Terraform
        uses: ./.github/actions/setup-terraform # è©³ç´°ã¯çœç•¥
      - name: Fetch AWS credentials
        uses: ./.github/actions/assume-role-with-oidc # è©³ç´°ã¯çœç•¥
      - name: Apply
        run: terraform apply -auto-approve
```

## ãƒ­ãƒ¼ã‚«ãƒ«ã«ã‚¤ãƒ³ãƒãƒ¼ãƒˆã™ã‚‹ã‚¹ã‚¯ãƒªãƒ—ãƒˆ

ã“ã®ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’ãƒ­ãƒ¼ã‚«ãƒ«ã§ä½•å›ã§ã‚‚å®Ÿè¡Œã™ã‚‹ã“ã¨ã§ã€ãƒ‡ãƒ¼ã‚¿ä¸€æ‹¬ä¿®æ­£ãªã©ã®ãƒãƒƒãƒã§ã‚‚æœ¬ç•ªãƒ‡ãƒ¼ã‚¿ã¨ã—ã¦ã®å†ç¾æ€§ã‚’æ®‹ã—ã¤ã¤ã€ãƒˆãƒ©ã‚¤ï¼†ã‚¨ãƒ©ãƒ¼ãŒä½•å›ã§ã‚‚ã§ãã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚

```bash
#!/usr/bin/env bash

aws s3 cp s3://${BUCKET_NAME}/database_dump.sql database_dump.sql

echo "Importing database_dump.sql..."
cat database_dump.sql | docker-compose exec -T mysql-dev mysql -u root -P 3306 development
```

ã“ã“ã¾ã§ã”ä¸€èª­ã„ãŸã ãã€ã‚ã‚ŠãŒã¨ã†ã”ã–ã„ã¾ã—ãŸï¼
